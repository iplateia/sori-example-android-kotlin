plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace 'com.iplateia.sori.example'
    compileSdk 35

    defaultConfig {
        applicationId "com.iplateia.sori.example"
        minSdk 24
        targetSdk 35
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // set SORI_APP_ID and SORI_SECRET_KEY from local.properties
        def soriAppId = localProperties['SORI_APP_ID']
        def soriSecretKey = localProperties['SORI_SECRET_KEY']
        resValue("string", "SORI_APP_ID", soriAppId)
        resValue("string", "SORI_SECRET_KEY", soriSecretKey)
    }

    signingConfigs {
        release {
            // uncomment below blocks to use your own keystore
             keyAlias keystoreProperties['keyAlias']
             keyPassword keystoreProperties['keyPassword']
             storeFile file(keystoreProperties['storeFile'])
             storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding = true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.22')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'com.google.guava:guava:33.4.8-android'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation "com.iplateia:sorisdk:4.1.6"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.9'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.8.9'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}